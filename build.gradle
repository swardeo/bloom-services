plugins {
    id 'idea'
    id 'java'
    id 'com.diffplug.spotless' version '5.8.2'
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_11

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    group 'money.bloom'

    dependencies {
        implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
        implementation platform('software.amazon.awssdk:bom:2.15.52')
        implementation 'software.amazon.awssdk:lambda'
        implementation 'software.amazon.awssdk:dynamodb'
        implementation 'software.amazon.awssdk:url-connection-client'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.0'
        implementation 'org.apache.logging.log4j:log4j-api:2.14.0'
        implementation 'org.apache.logging.log4j:log4j-core:2.14.0'
        runtimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.14.0'
        runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.2.0'

        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
        testImplementation 'org.assertj:assertj-core:3.18.1'
        testImplementation 'org.mockito:mockito-core:3.6.28'
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            googleJavaFormat().aosp()
        }
    }

    task buildZip(type: Zip) {
        from compileJava
        from processResources
        into('lib') {
            from configurations.runtimeClasspath
        }
        destinationDirectory.set(file("$rootDir/build/artifacts"))
    }

    buildZip.dependsOn build

    task printVersion {
        doLast{
            println project.version
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}
